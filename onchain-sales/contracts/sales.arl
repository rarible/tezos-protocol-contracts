archetype sales(
    owner: address,
    protocol_fee: nat,
    transfer_manager: address,
    sales_storage: address
)
with metadata ""

constant SALE_ALREADY_EXISTS: string = "SALE_ALREADY_EXISTS"
constant MISSING_SALE: string = "MISSING_SALE"
constant MISSING_BUNDLE_SALE: string = "MISSING_BUNDLE_SALE"
constant AMOUNT_MISMATCH: string = "AMOUNT_MISMATCH"
constant CANT_UNPACK_FA2_ASSET: string = "CANT_UNPACK_FA2_ASSET"
constant CANT_UNPACK_FA12_ASSET: string = "CANT_UNPACK_FA12_ASSET"
constant CANT_UNPACK_BUNDLE: string = "CANT_UNPACK_BUNDLE"
constant WRONG_XTZ_PAYLOAD: string = "WRONG_XTZ_PAYLOAD"
constant MAX_BUNDLE_SIZE: string = "MAX_BUNDLE_SIZE"
constant INVALID_BUNDLE_ITEM_QTY: string = "INVALID_BUNDLE_ITEM_QTY"
constant BUNDLE_CANT_BE_EMPTY: string = "BUNDLE_CANT_BE_EMPTY"
constant FEES_OVER_SELLER_LIMIT: string = "FEES_OVER_SELLER_LIMIT"
constant INVALID_SALE_END_DATE: string = "INVALID_SALE_END_DATE"
constant INVALID_SALE_START_DATE: string = "INVALID_SALE_START_DATE"
constant SALE_CLOSED: string = "SALE_CLOSED"
constant SALE_NOT_STARTED: string = "SALE_NOT_STARTED"
constant INVALID_BUY_AMOUNT: string = "INVALID_BUY_AMOUNT"

//----------------------------------------------------------------------------
// Owner role transfer
//----------------------------------------------------------------------------

variable owner_candidate : option<address> = none
variable max_bundle_items : nat = 10
variable max_fees_limit : nat = 10000

entry declare_ownership(candidate : address) {
  called by owner
  effect {
    owner_candidate := some(candidate);
  }
}

entry claim_ownership() {
  called by opt_get(owner_candidate)
  effect {
    owner := opt_get(owner_candidate);
    owner_candidate := none
  }
}


//----------------------------------------------------------------------------
// Admin
//----------------------------------------------------------------------------
entry set_sales_storage_contract(sesc_contract : address) {
  called by owner
  effect {
    sales_storage := sesc_contract;
  }
}

entry set_protocol_fee(spf : nat) {
  called by owner
  effect {
    protocol_fee := spf;
  }
}

entry set_fees_limit(sfl : nat) {
  called by owner
  effect {
    max_fees_limit := sfl;
  }
}

entry set_transfer_manager(stm_contract : address) {
  called by owner
  effect {
    transfer_manager := stm_contract;
  }
}

entry set_sales_storage(sss_contract : address) {
  called by owner
  effect {
    sales_storage := sss_contract;
  }
}

entry set_max_bundle_items(smbi_number : nat) {
  called by owner
  effect {
    max_bundle_items := smbi_number;
  }
}

//----------------------------------------------------------------------------
// Types
//----------------------------------------------------------------------------
enum asset_type =
| XTZ
| FA12
| FA2

record FA2_asset {
  fa2_asset_contract : address;
  fa2_asset_token_id: nat;
}

record FA12_asset {
  fa12_asset_contract : address;
}

record transfer_param {
  destination_address: address;
  token_id: nat;
  token_amount: nat
} as ((%to, (token_id, amount)))

record process_transfer_param {
  ptp_asset_type: asset_type;
  ptp_asset: bytes;
  ptp_amount: nat;
  ptp_origin: address;
  ptp_destination: address;
}

record part {
  part_account : address;
  part_value   : nat;
}

record fees {
  origin_fees : list<part>;
  payouts: list<part>;
}

record bundle_item {
  bundle_item_contract : address;
  bundle_item_id   : nat;
  bundle_item_qty: nat;
}

record bundle_sale {
  bundle_sale_origin_fees : list<part>;
  bundle_sale_payouts : list<part>;
  bundle_sale_amount : nat;
  bundle_sale_start: option<date>;
  bundle_sale_end: option<date>;
  bundle_sale_qty: nat;
  bundle_sale_max_fees_base_boint: nat;
  bundle_sale_data_type  : option<bytes>;
  bundle_sale_data       : option<bytes>;
}

record sale {
  sale_origin_fees : list<part>;
  sale_payouts : list<part>;
  sale_amount : nat;
  sale_asset_qty: nat;
  sale_start: option<date>;
  sale_end: option<date>;
  sale_max_fees_base_boint: nat;
  sale_data_type  : option<bytes>;
  sale_data       : option<bytes>;
}

entry sell (
  s_asset_contract: address,
  s_asset_token_id: nat,
  s_sale_type: asset_type,
  s_sale_asset: bytes,
  s_sale : sale) {
    require {
        r_s0: s_sale.sale_amount > 0;
        r_s1: s_sale.sale_asset_qty > 0;
        r_s2: s_sale.sale_max_fees_base_boint > 0
                and s_sale.sale_max_fees_base_boint <= max_fees_limit
                and s_sale.sale_max_fees_base_boint > protocol_fee
                and process_sum_fees(s_sale.sale_origin_fees) + protocol_fee <= s_sale.sale_max_fees_base_boint;
        r_s3: s_sale_type = FA2
            or
            s_sale_type = FA12
            or
            s_sale_type = XTZ;
    }
    effect {
      if(issome(s_sale.sale_end)) then (
        var end_date = opt_get(s_sale.sale_end);
        dorequire(end_date > now, INVALID_SALE_END_DATE);
        if(issome(s_sale.sale_start)) then (
          var start_date = opt_get(s_sale.sale_start);
          dorequire(start_date < end_date, INVALID_SALE_START_DATE)
        );
      );

      var unpack_result = verify_and_unpack_asset(s_sale_type, s_sale_asset);
      transfer 0tz to sales_storage call set_sale<address * nat * address * asset_type * bytes * sale>(
        (
          s_asset_contract,
          s_asset_token_id,
          caller,
          s_sale_type,
          s_sale_asset,
          s_sale
        )
      );
    }
}

entry sell_bundle (
  sb_bundle: bytes,
  sb_sale_type: asset_type,
  sb_sale_asset: bytes,
  sb_sale : bundle_sale) {
    require {
        r_sb0: sb_sale.bundle_sale_amount > 0;
        r_sb1: sb_sale.bundle_sale_max_fees_base_boint > 0
              and sb_sale.bundle_sale_max_fees_base_boint <= max_fees_limit
              and sb_sale.bundle_sale_max_fees_base_boint > protocol_fee
              and process_sum_fees(sb_sale.bundle_sale_origin_fees) + protocol_fee <= sb_sale.bundle_sale_max_fees_base_boint;
        r_sb2: sb_sale_type = FA2
            or
            sb_sale_type = FA12
            or
            sb_sale_type = XTZ;
    }
    effect {
      if(issome(sb_sale.bundle_sale_end)) then (
        var end_date = opt_get(sb_sale.bundle_sale_end);
        dorequire(end_date > now, INVALID_SALE_END_DATE);
        if(issome(sb_sale.bundle_sale_start)) then (
          var start_date = opt_get(sb_sale.bundle_sale_start);
          dorequire(start_date < end_date, INVALID_SALE_START_DATE)
        );
      );
      var unpack_result = verify_and_unpack_asset(sb_sale_type, sb_sale_asset);
      var bundle = verify_and_unpack_bundle(sb_bundle);
      dorequire(length(bundle) <= max_bundle_items, (MAX_BUNDLE_SIZE, max_bundle_items));
      transfer 0tz to sales_storage call set_bundle_sale<bytes * address * asset_type * bytes * bundle_sale>(
        (
          sb_bundle,
          caller,
          sb_sale_type,
          sb_sale_asset,
          sb_sale
        )
      );
    }
}


entry buy(
  b_asset_contract: address,
  b_asset_token_id: nat,
  b_seller: address,
  b_sale_type: asset_type,
  b_sale_asset: bytes,
  b_amount: nat,
  b_origin_fees: list<part>,
  b_payouts: list<part>){
    effect {
      var current_sale = require_some(get_sale(b_asset_contract, b_asset_token_id, b_seller, b_sale_type, b_sale_asset), MISSING_SALE);
      dorequire(process_sum_fees(current_sale.sale_origin_fees) + process_sum_fees(b_origin_fees) + protocol_fee <= current_sale.sale_max_fees_base_boint, FEES_OVER_SELLER_LIMIT);
      dorequire(b_amount > 0 and b_amount <= current_sale.sale_asset_qty, INVALID_BUY_AMOUNT);

      if(issome(current_sale.sale_end)) then (
        var end_date = opt_get(current_sale.sale_end);
        dorequire(end_date > now, SALE_CLOSED);
      );

      if(issome(current_sale.sale_start)) then (
          var start_date = opt_get(current_sale.sale_start);
          dorequire(start_date < now, SALE_NOT_STARTED);
      );

      var unpack_result = verify_and_unpack_asset(b_sale_type, b_sale_asset);
      var buy_asset_contract = unpack_result[0];
      var buy_asset_token_id = unpack_result[1];

      if(b_sale_type = XTZ) then (
        dorequire(transferred = current_sale.sale_amount * b_amount * 1utz, (AMOUNT_MISMATCH, transferred, current_sale.sale_amount * 1utz))
      );

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          [{
            bundle_item_contract = b_asset_contract;
            bundle_item_id = b_asset_token_id ;
            bundle_item_qty = current_sale.sale_asset_qty
          }],
          b_seller,
          caller,
          current_sale.sale_amount * b_amount,
          b_sale_type,
          b_sale_asset,
          caller,
          b_seller,
          sales_storage,
          protocol_fee,
          current_sale.sale_origin_fees.concat(b_origin_fees),
          current_sale.sale_payouts.concat(b_payouts)
        )
      );
      if(b_amount = current_sale.sale_asset_qty) then (
        transfer 0tz to sales_storage call remove_sale<address * nat * address * asset_type * bytes>((b_asset_contract, b_asset_token_id, b_seller, b_sale_type, b_sale_asset))
      ) else (
        current_sale.sale_asset_qty := abs(current_sale.sale_asset_qty - b_amount);
        transfer 0tz to sales_storage call set_sale<address * nat * address * asset_type * bytes * sale>(
          (
            b_asset_contract,
            b_asset_token_id,
            b_seller,
            b_sale_type,
            b_sale_asset,
            current_sale
          )
        );
      )
    }
}

entry buy_bundle(
  bb_bundle: bytes,
  bb_seller: address,
  bb_sale_type: asset_type,
  bb_sale_asset: bytes,
  bb_amount: nat,
  bb_origin_fees: list<part>,
  bb_payouts: list<part>
  ){
    effect {
      var current_sale = require_some(get_bundle_sale(bb_bundle, bb_seller, bb_sale_type, bb_sale_asset), MISSING_BUNDLE_SALE);
      dorequire(process_sum_fees(current_sale.bundle_sale_origin_fees) + process_sum_fees(bb_origin_fees) + protocol_fee <= current_sale.bundle_sale_max_fees_base_boint, FEES_OVER_SELLER_LIMIT);
      dorequire(bb_amount > 0 and bb_amount <= current_sale.bundle_sale_qty, INVALID_BUY_AMOUNT);

      if(issome(current_sale.bundle_sale_end)) then (
        var end_date = opt_get(current_sale.bundle_sale_end);
        dorequire(end_date > now, SALE_CLOSED);
      );

      if(issome(current_sale.bundle_sale_start)) then (
          var start_date = opt_get(current_sale.bundle_sale_start);
          dorequire(start_date < now, SALE_NOT_STARTED);
      );

      var unpacked_bundle = verify_and_unpack_bundle(bb_bundle);

      if(bb_sale_type = XTZ) then (
        dorequire(transferred = current_sale.bundle_sale_amount * bb_amount * 1utz, (AMOUNT_MISMATCH, transferred, current_sale.bundle_sale_amount * 1utz))
      );

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          unpacked_bundle,
          bb_seller,
          caller,
          current_sale.bundle_sale_amount,
          bb_sale_type,
          bb_sale_asset,
          caller,
          bb_seller,
          sales_storage,
          protocol_fee,
          current_sale.bundle_sale_origin_fees.concat(bb_origin_fees),
          current_sale.bundle_sale_payouts.concat(bb_payouts)
        )
      );

      if(bb_amount = current_sale.bundle_sale_qty) then (
        transfer 0tz to sales_storage call remove_bundle_sale<bytes * address * asset_type * bytes>((bb_bundle, bb_seller, bb_sale_type, bb_sale_asset))
      ) else (
        current_sale.bundle_sale_qty := abs(current_sale.bundle_sale_qty - bb_amount);
        transfer 0tz to sales_storage call set_bundle_sale<bytes * address * asset_type * bytes * bundle_sale>(
          (
            bb_bundle,
            bb_seller,
            bb_sale_type,
            bb_sale_asset,
            current_sale
          )
        );
      );
    }
}

entry cancel_sale(
  cs_asset_contract: address,
  cs_asset_token_id: nat,
  cs_sale_type: asset_type,
  cs_sale_asset: bytes){
    require {
        r_cs0: sale_exists(cs_asset_contract, cs_asset_token_id, caller, cs_sale_type, cs_sale_asset) = true;
    }
    effect {
      transfer 0tz to sales_storage call remove_sale<address * nat * address * asset_type * bytes>((cs_asset_contract, cs_asset_token_id, caller, cs_sale_type, cs_sale_asset))
    }
}

entry cancel_bundle_sale(
  cbs_bundle: bytes,
  cbs_sale_type: asset_type,
  cbs_sale_asset: bytes){
    require {
        r_cbs0: bundle_sale_exists(cbs_bundle, caller, cbs_sale_type, cbs_sale_asset) = true;
    }
    effect {
      transfer 0tz to sales_storage call remove_bundle_sale<bytes * address * asset_type * bytes>((cbs_bundle, caller, cbs_sale_type, cbs_sale_asset))
    }
}

function sale_exists(
  se_asset_contract: address,
  se_asset_token_id: nat,
  se_seller: address,
  se_sale_type: asset_type,
  se_sale_asset: bytes) : bool {
    return match callview<bool>(
        sales_storage,
        "sale_exists",
        (
            se_asset_contract,
            se_asset_token_id,
            se_seller,
            se_sale_type,
            se_sale_asset
        )
    )
    with
        | some(v) -> v
        | none -> false
    end
}

function bundle_sale_exists(
  bse_bundle: bytes,
  bse_seller: address,
  bse_sale_type: asset_type,
  bse_sale_asset: bytes) : bool {
    return match callview<bool>(
        sales_storage,
        "bundle_sale_exists",
        (
            bse_bundle,
            bse_seller,
            bse_sale_type,
            bse_sale_asset
        )
    )
    with
        | some(v) -> v
        | none -> false
    end
}

function verify_and_unpack_asset(ua_asset_type: asset_type, ua_asset_data: bytes) : (option<address> * option<nat>) {
    var buy_asset_contract: option<address> = none;
    var buy_asset_token_id: option<nat> = none;
    match ua_asset_type with
      | FA2 -> (
        match unpack<FA2_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa2_asset_contract);
            buy_asset_token_id := some(buyAsset.fa2_asset_token_id);
          )
        | none -> fail(CANT_UNPACK_FA2_ASSET)
        end
      )
      | FA12 -> (
        match unpack<FA12_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa12_asset_contract);
        )
        | none -> fail(CANT_UNPACK_FA12_ASSET)
        end
      )
      | XTZ -> (
          if(ua_asset_data <> 0x) then (
            fail(WRONG_XTZ_PAYLOAD)
          )
      )
      end;
      return ((buy_asset_contract, buy_asset_token_id))
}

function verify_and_unpack_bundle(vaub_data: bytes) : (list<bundle_item>) {
    var bundle: list<bundle_item> = [];
    match unpack<list<bundle_item>>(vaub_data) with
    | some(unpacked_bundle) -> (
        bundle := unpacked_bundle;
    )
    | none -> fail(CANT_UNPACK_BUNDLE)
    end;
    dorequire(length(bundle) > 0, BUNDLE_CANT_BE_EMPTY);
    for item in bundle do
      dorequire(item.bundle_item_qty > 0, INVALID_BUNDLE_ITEM_QTY);
    done;
    return (bundle)
}

function get_sale(
  gb_asset_contract: address,
  gb_asset_token_id: nat,
  gb_seller: address,
  gb_sale_type: asset_type,
  gb_sale_asset: bytes) : option<sale> {
    return opt_get(callview<option<sale>>(
        sales_storage,
        "get_sale",
        (
          gb_asset_contract,
          gb_asset_token_id,
          gb_seller,
          gb_sale_type,
          gb_sale_asset
        )
    ))
}

function get_bundle_sale(
  gbs_bundle: bytes,
  gbs_seller: address,
  gbs_sale_type: asset_type,
  gbs_sale_asset: bytes) : option<bundle_sale> {
    return opt_get(callview<option<bundle_sale>>(
        sales_storage,
        "get_bundle_sale",
        (
          gbs_bundle,
          gbs_seller,
          gbs_sale_type,
          gbs_sale_asset
        )
    ))
}

function bp(bp_value : nat, bp_op_value : nat) : nat {
  return abs(floor(bp_value * bp_op_value / 10000))
}

function process_sum_fees(psf_fees: list<part>): nat {
  var total = 0;
  for fee in psf_fees do
    total := total + fee.part_value;
  done;
  return total
}


entry default() {}