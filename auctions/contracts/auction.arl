archetype auction(
    owner: address,
    protocol_fee: nat,
    transfer_manager: address,
    auction_storage: address
)
with metadata ""

constant AUCTION_ALREADY_EXISTS           : string = "AUCTION_ALREADY_EXISTS"
constant AUCTION_BID_ALREADY_EXISTS       : string = "AUCTION_BID_ALREADY_EXISTS"
constant AUCTION_BID_AMOUNT_MISMATCH      : string = "AUCTION_BID_AMOUNT_MISMATCH"
constant AUCTION_BID_TOO_LOW              : string = "AUCTION_BID_TOO_LOW"
constant AUCTION_FINISHED                 : string = "AUCTION_FINISHED"
constant AUCTION_NON_CANCELLABLE          : string = "AUCTION_NON_CANCELLABLE"
constant AUCTION_NOT_FINISHABLE           : string = "AUCTION_NOT_FINISHABLE"
constant AUCTION_NOT_IN_PROGRESS          : string = "AUCTION_NOT_IN_PROGRESS"
constant AUCTION_START_DATE_IN_THE_PAST   : string = "AUCTION_START_DATE_IN_THE_PAST"
constant AUCTION_WITH_BID_NON_CANCELLABLE : string = "AUCTION_WITH_BID_NON_CANCELLABLE"
constant BUNDLE_CANT_BE_EMPTY             : string = "BUNDLE_CANT_BE_EMPTY"
constant CANT_UNPACK_BUNDLE               : string = "CANT_UNPACK_BUNDLE"
constant CANT_UNPACK_FA12_BUY_ASSET       : string = "CANT_UNPACK_FA12_BUY_ASSET"
constant CANT_UNPACK_FA2_BUY_ASSET        : string = "CANT_UNPACK_FA2_BUY_ASSET"
constant FEES_OVER_SELLER_LIMIT           : string = "FEES_OVER_SELLER_LIMIT"
constant INVALID_BUNDLE_ITEM_QTY          : string = "INVALID_BUNDLE_ITEM_QTY"
constant INVALID_CALLER                   : string = "INVALID_CALLER"
constant MAX_BUNDLE_SIZE                  : string = "MAX_BUNDLE_SIZE"
constant MISSING_AUCTION                  : string = "MISSING_AUCTION"
constant MISSING_OWNER_CANDIDATE          : string = "MISSING_OWNER_CANDIDATE"
constant NO_BID_FOR_AUCTION               : string = "NO_BID_FOR_AUCTION"
constant NOT_FOUND                        : string = "NOT_FOUND"
constant ONLY_SELLER_CAN_CANCEL_AUCTION   : string = "ONLY_SELLER_CAN_CANCEL_AUCTION"
constant WRONG_BUY_ASSET_PAYLOAD          : string = "WRONG_BUY_ASSET_PAYLOAD"

//----------------------------------------------------------------------------
// Owner role transfer
//----------------------------------------------------------------------------

variable owner_candidate : option<address> = none
variable max_bundle_items : nat = 10
variable max_fees_limit : nat = 10000

entry declare_ownership(candidate : address) {
  called by owner
  require {
    r_dc_0: transferred = 0utz;
  }
  effect {
    owner_candidate := some(candidate);
  }
}

entry claim_ownership() {
  constant {
    owner_candidate_addr ?is owner_candidate otherwise MISSING_OWNER_CANDIDATE;
  }
  require {
    co1: owner_candidate_addr = caller otherwise INVALID_CALLER;
    co2: transferred = 0utz;
  }
  effect {
    owner := owner_candidate_addr;
    owner_candidate := none
  }
}

//----------------------------------------------------------------------------
// Auction variables
//----------------------------------------------------------------------------
variable extension_duration : duration = 15m
variable max_duration : duration = 1000d


//----------------------------------------------------------------------------
// Admin
//----------------------------------------------------------------------------
entry set_auction_storage_contract(sacs_contract : address) {
  called by owner
  require {
    r_sacs_0: transferred = 0utz;
  }
  effect {
    auction_storage := sacs_contract;
  }
}

entry set_extension_duration(sed : duration) {
  called by owner
  require {
    r_sed_0: transferred = 0utz;
  }
  effect {
    extension_duration := sed;
  }
}

entry set_max_duration(smd : duration) {
  called by owner
  require {
    r_md_0: transferred = 0utz;
  }
  effect {
    max_duration := smd;
  }
}

entry set_protocol_fee(spf : nat) {
  called by owner
  require {
    r_spf_0: transferred = 0utz;
  }
  effect {
    protocol_fee := spf;
  }
}

entry set_max_bundle_items(smbi_number : nat) {
  called by owner
  require {
    r_smbi_0: transferred = 0utz;
  }
  effect {
    max_bundle_items := smbi_number;
  }
}

entry set_fees_limit(sfl : nat) {
  called by owner
  require {
    r_sfl_0: transferred = 0utz;
  }
  effect {
    max_fees_limit := sfl;
  }
}

entry set_transfer_manager(stm_contract : address) {
  called by owner
  require {
    r_stm_0: transferred = 0utz;
  }
  effect {
    transfer_manager := stm_contract;
  }
}

//----------------------------------------------------------------------------
// Types
//----------------------------------------------------------------------------
enum asset_type =
| XTZ
| FA12
| FA2


record FA2_asset {
  fa2_asset_contract : address;
  fa2_asset_token_id: nat;
}

record FA12_asset {
  fa12_asset_contract : address;
}

record transfer_param {
  destination_address: address;
  token_id: nat;
  token_amount: nat
} as ((%to, (token_id, amount)))

record process_transfer_param {
  ptp_asset_type: asset_type;
  ptp_asset: bytes;
  ptp_amount: nat;
  ptp_origin: address;
  ptp_destination: address;
}

record part {
  part_account : address;
  part_value   : nat;
}

record fees {
  origin_fees : list<part>;
  payouts: list<part>;
}

record bundle_item {
  bundle_item_contract : address;
  bundle_item_id   : nat;
  bundle_item_qty: nat;
}

record bid {
  bid_origin_fees : list<part>;
  bid_payouts : list<part>;
  bid_amount : nat;
  bid_bidder: address;
  bid_data_type  : option<bytes>;
  bid_data       : option<bytes>;
}

record auction_record {
  auction_sell_asset_amount: nat;
  auction_buy_asset_type : asset_type;
  auction_buy_asset : bytes;
  auction_last_bid: option<bid>;
  auction_start_time: date;
  auction_end_time: date;
  auction_minimal_price: nat;
  auction_buy_out_price: nat;
  auction_minimal_step: nat;
  auction_max_seller_fees: nat;
  auction_origin_fees : list<part>;
  auction_payouts : list<part>;
  auction_data_type  : option<bytes>;
  auction_data       : option<bytes>;
}

record bundle_auction_record {
  bar_buy_asset_type : asset_type;
  bar_buy_asset : bytes;
  bar_last_bid: option<bid>;
  bar_start_time: date;
  bar_end_time: date;
  bar_minimal_price: nat;
  bar_buy_out_price: nat;
  bar_minimal_step: nat;
  bar_max_seller_fees: nat;
  bar_origin_fees : list<part>;
  bar_payouts : list<part>;
  bar_data_type  : option<bytes>;
  bar_data       : option<bytes>;
}

record auction_args {
  auction_args_sell_asset_amount: nat;
  auction_args_buy_asset_type : asset_type;
  auction_args_buy_asset : bytes;
  auction_args_start_time: option<date>;
  auction_args_duration: duration;
  auction_args_minimal_price: nat;
  auction_args_buy_out_price: nat;
  auction_args_minimal_step: nat;
  auction_args_max_seller_fees: nat;
  auction_args_origin_fees : list<part>;
  auction_args_payouts : list<part>;
  auction_args_data_type  : option<bytes>;
  auction_args_data       : option<bytes>;
}

record bundle_auction_args {
  bundle_auction_args_buy_asset_type : asset_type;
  bundle_auction_args_buy_asset : bytes;
  bundle_auction_args_start_time: option<date>;
  bundle_auction_args_duration: duration;
  bundle_auction_args_minimal_price: nat;
  bundle_auction_args_buy_out_price: nat;
  bundle_auction_args_minimal_step: nat;
  bundle_auction_max_seller_fees: nat;
  bundle_auction_args_origin_fees : list<part>;
  bundle_auction_args_payouts : list<part>;
  bundle_auction_args_data_type  : option<bytes>;
  bundle_auction_args_data       : option<bytes>;
}

//----------------------------------------------------------------------------
// Auction entries
//----------------------------------------------------------------------------

entry start_auction(
    sa_asset_contract : address,
    sa_asset_token_id : nat,
    sa_auction: auction_args
) {
    require{
        r_sa0: transferred = 0utz;
        r_sa1: sa_auction.auction_args_buy_asset_type = FA2
            or
            sa_auction.auction_args_buy_asset_type = FA12
            or
            sa_auction.auction_args_buy_asset_type = XTZ;
        r_sa2: sa_auction.auction_args_duration > extension_duration;
        r_sa3: sa_auction.auction_args_duration < max_duration;
        r_sa4: sa_auction.auction_args_buy_out_price > sa_auction.auction_args_minimal_price;
        r_sa5: sa_auction.auction_args_minimal_price > 0;
        r_sa6: sa_auction.auction_args_minimal_step > 0;
        r_sa7: sa_auction.auction_args_sell_asset_amount > 0;
        r_sa8: auction_exists(sa_asset_contract, sa_asset_token_id, caller) = false;
        r_sa9: sa_auction.auction_args_max_seller_fees > 0
                and sa_auction.auction_args_max_seller_fees <= max_fees_limit
                and sa_auction.auction_args_max_seller_fees > protocol_fee
                and process_sum_fees(sa_auction.auction_args_origin_fees) + protocol_fee <= sa_auction.auction_args_max_seller_fees;
    }
    effect {
        const processed_asset = verify_and_unpack_asset(sa_auction.auction_args_buy_asset_type, sa_auction.auction_args_buy_asset);

        var start_time: date = now;
        var end_time: date = now;
        if (sa_auction.auction_args_start_time <> none<date>) then (
            start_time ?:= sa_auction.auction_args_start_time : NOT_FOUND;
            do_require(start_time >= now, AUCTION_START_DATE_IN_THE_PAST);
            end_time := start_time + sa_auction.auction_args_duration;
        ) else (
            start_time := now;
            end_time := now + sa_auction.auction_args_duration;
        );

        const storage_auction: auction_record = {
            auction_sell_asset_amount = sa_auction.auction_args_sell_asset_amount;
            auction_buy_asset = sa_auction.auction_args_buy_asset;
            auction_buy_asset_type = sa_auction.auction_args_buy_asset_type;
            auction_last_bid = none;
            auction_start_time = start_time;
            auction_end_time = end_time;
            auction_minimal_price = sa_auction.auction_args_minimal_price;
            auction_buy_out_price = sa_auction.auction_args_buy_out_price;
            auction_minimal_step = sa_auction.auction_args_minimal_step;
            auction_max_seller_fees = sa_auction.auction_args_max_seller_fees;
            auction_payouts = sa_auction.auction_args_payouts;
            auction_origin_fees = sa_auction.auction_args_origin_fees;
            auction_data_type = sa_auction.auction_args_data_type;
            auction_data = sa_auction.auction_args_data
        };

        transfer 0tz to auction_storage call set_auction<address * nat * address * auction_record>(
          (sa_asset_contract, sa_asset_token_id, caller, storage_auction)
        );
        transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([{
          ptp_asset_type = FA2;
          ptp_asset = pack({
            fa2_asset_contract = sa_asset_contract;
            fa2_asset_token_id = sa_asset_token_id
            });
          ptp_amount = sa_auction.auction_args_sell_asset_amount;
          ptp_origin = caller;
          ptp_destination = auction_storage
        }]);
    }
}

entry start_bundle_auction(
    sba_bundle : bytes,
    sba_bundle_auction: bundle_auction_args
) {
    require{
        r_sba0: transferred = 0utz;
        r_sba1: sba_bundle_auction.bundle_auction_args_buy_asset_type = FA2
            or
            sba_bundle_auction.bundle_auction_args_buy_asset_type = FA12
            or
            sba_bundle_auction.bundle_auction_args_buy_asset_type = XTZ;
        r_sba2: sba_bundle_auction.bundle_auction_args_duration > extension_duration;
        r_sba3: sba_bundle_auction.bundle_auction_args_duration < max_duration;
        r_sba4: sba_bundle_auction.bundle_auction_args_buy_out_price > sba_bundle_auction.bundle_auction_args_minimal_price;
        r_sba5: sba_bundle_auction.bundle_auction_args_minimal_price > 0;
        r_sba6: sba_bundle_auction.bundle_auction_args_minimal_step > 0;
        r_sba7: bundle_auction_exists(sba_bundle, caller) = false;
        r_sba8: sba_bundle_auction.bundle_auction_max_seller_fees > 0
                and sba_bundle_auction.bundle_auction_max_seller_fees <= max_fees_limit
                and sba_bundle_auction.bundle_auction_max_seller_fees > protocol_fee
                and process_sum_fees(sba_bundle_auction.bundle_auction_args_origin_fees) + protocol_fee <= sba_bundle_auction.bundle_auction_max_seller_fees;
    }
    effect {
        const processed_asset = verify_and_unpack_asset(sba_bundle_auction.bundle_auction_args_buy_asset_type, sba_bundle_auction.bundle_auction_args_buy_asset);
        const unpacked_bundle = verify_and_unpack_bundle(sba_bundle);
        do_require(length(unpacked_bundle) <= max_bundle_items, (MAX_BUNDLE_SIZE, max_bundle_items));

        var start_time: date = now;
        var end_time: date = now;
        if (sba_bundle_auction.bundle_auction_args_start_time <> none<date>) then (
            start_time ?:= sba_bundle_auction.bundle_auction_args_start_time : NOT_FOUND;
            do_require(start_time >= now, AUCTION_START_DATE_IN_THE_PAST);
            end_time := start_time + sba_bundle_auction.bundle_auction_args_duration;
        ) else (
            start_time := now;
            end_time := now + sba_bundle_auction.bundle_auction_args_duration;
        );

        const storage_auction: bundle_auction_record = {
            bar_buy_asset = sba_bundle_auction.bundle_auction_args_buy_asset;
            bar_buy_asset_type = sba_bundle_auction.bundle_auction_args_buy_asset_type;
            bar_last_bid = none;
            bar_start_time = start_time;
            bar_end_time = end_time;
            bar_minimal_price = sba_bundle_auction.bundle_auction_args_minimal_price;
            bar_buy_out_price = sba_bundle_auction.bundle_auction_args_buy_out_price;
            bar_minimal_step = sba_bundle_auction.bundle_auction_args_minimal_step;
            bar_max_seller_fees = sba_bundle_auction.bundle_auction_max_seller_fees;
            bar_payouts = sba_bundle_auction.bundle_auction_args_payouts;
            bar_origin_fees = sba_bundle_auction.bundle_auction_args_origin_fees;
            bar_data_type = sba_bundle_auction.bundle_auction_args_data_type;
            bar_data = sba_bundle_auction.bundle_auction_args_data
        };

        transfer 0tz to auction_storage call set_bundle_auction<bytes * address * bundle_auction_record>(
          (sba_bundle, caller, storage_auction)
        );

        for item in unpacked_bundle do
          transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = FA2;
            ptp_asset = pack({
              fa2_asset_contract = item.bundle_item_contract;
              fa2_asset_token_id = item.bundle_item_id
              });
            ptp_amount = item.bundle_item_qty;
            ptp_origin = caller;
            ptp_destination = auction_storage
          }]);
        done;

    }
}

entry put_bid(pb_asset_contract: address, pb_asset_id: nat, pb_asset_seller: address, pb_bid: bid){
    require {
        r_pb0: pb_bid.bid_amount > 0;
        r_pb1: pb_bid.bid_bidder = caller;
    }
    effect {
      const auction ?= get_auction_record(pb_asset_contract, pb_asset_id, pb_asset_seller) : MISSING_AUCTION;
      const is_xtz = (auction.auction_buy_asset_type = XTZ);
      const previous_bid = auction.auction_last_bid;
      const is_buyout = auction.auction_buy_out_price > 0 and pb_bid.bid_amount >= auction.auction_buy_out_price;
      var buy_asset_contract: option<address> = none;
      var buy_asset_token_id: option<nat> = none;

      do_require(auction.auction_start_time < now, AUCTION_NOT_IN_PROGRESS);
      do_require(auction.auction_end_time > now, AUCTION_FINISHED);
      do_require(process_sum_fees(auction.auction_origin_fees) + process_sum_fees(pb_bid.bid_origin_fees) + protocol_fee <= auction.auction_max_seller_fees, FEES_OVER_SELLER_LIMIT);

      const processed_asset = verify_and_unpack_asset(auction.auction_buy_asset_type, auction.auction_buy_asset);
      buy_asset_contract := processed_asset[0];
      buy_asset_token_id := processed_asset[1];

      if(is_xtz) then (
        do_require(transferred = (pb_bid.bid_amount * 1utz), (AUCTION_BID_AMOUNT_MISMATCH, transferred, pb_bid.bid_amount * 1utz));
      );

      if(is_buyout = true) then (
        const origin = pb_asset_seller;
        transfer 0tz to auction_storage call remove_auction<address * nat * address>((pb_asset_contract, pb_asset_id, pb_asset_seller));
        transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          [{
            bundle_item_contract = pb_asset_contract;
            bundle_item_id = pb_asset_id ;
            bundle_item_qty = auction.auction_sell_asset_amount
          }],
          auction_storage,
          pb_bid.bid_bidder,
          pb_bid.bid_amount,
          auction.auction_buy_asset_type,
          auction.auction_buy_asset,
          caller,
          pb_asset_seller,
          auction_storage,
          protocol_fee,
          auction.auction_origin_fees.concat(pb_bid.bid_origin_fees),
          auction.auction_payouts.concat(pb_bid.bid_payouts))
        );

      ) else (
        if(is_xtz = false) then (
          if(
            auction.auction_end_time - now < extension_duration and
            auction.auction_end_time + extension_duration < auction.auction_start_time + max_duration
          ) then (
            auction.auction_end_time := now + extension_duration;
          );
          auction.auction_last_bid := some(pb_bid);
          transfer transferred to auction_storage call set_auction<address * nat * address * auction_record>((pb_asset_contract, pb_asset_id, pb_asset_seller, auction));
          transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([
            {
              ptp_asset_type = auction.auction_buy_asset_type;
              ptp_asset = auction.auction_buy_asset;
              ptp_amount = pb_bid.bid_amount;
              ptp_origin = pb_bid.bid_bidder;
              ptp_destination = auction_storage
            }
          ]);
        );
      );

      if(is_none(previous_bid)) then (
        do_require(auction.auction_minimal_price <= pb_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(auction.auction_minimal_step <= pb_bid.bid_amount, AUCTION_BID_TOO_LOW);
      ) else (
        const last_bid ?= previous_bid : NOT_FOUND;
        do_require(last_bid.bid_amount < pb_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(auction.auction_minimal_step <= pb_bid.bid_amount - last_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(last_bid.bid_bidder <> pb_bid.bid_bidder, AUCTION_BID_ALREADY_EXISTS);
        transfer 0tz to auction_storage call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = auction.auction_buy_asset_type;
            ptp_asset = auction.auction_buy_asset;
            ptp_amount = last_bid.bid_amount;
            ptp_origin = auction_storage;
            ptp_destination = last_bid.bid_bidder
          }]
        );
      );
    }
}

entry put_bundle_bid(pbb_bundle: bytes, pbb_asset_seller: address, pbb_bid: bid){
    require {
        r_pbb0: pbb_bid.bid_amount > 0;
        r_pbb1: pbb_bid.bid_bidder = caller;
    }
    effect {
      const auction ?= get_bundle_auction_record(pbb_bundle, pbb_asset_seller) : MISSING_AUCTION;
      const is_xtz = (auction.bar_buy_asset_type = XTZ);
      const previous_bid = auction.bar_last_bid;
      const is_buyout = auction.bar_buy_out_price > 0 and pbb_bid.bid_amount >= auction.bar_buy_out_price;
      var buy_asset_contract: option<address> = none;
      var buy_asset_token_id: option<nat> = none;
      const bundle = verify_and_unpack_bundle(pbb_bundle);

      do_require(auction.bar_start_time < now, AUCTION_NOT_IN_PROGRESS);
      do_require(auction.bar_end_time > now, AUCTION_FINISHED);
      do_require(process_sum_fees(auction.bar_origin_fees) + process_sum_fees(pbb_bid.bid_origin_fees) + protocol_fee <= auction.bar_max_seller_fees, FEES_OVER_SELLER_LIMIT);

      const processed_asset = verify_and_unpack_asset(auction.bar_buy_asset_type, auction.bar_buy_asset);
      buy_asset_contract := processed_asset[0];
      buy_asset_token_id := processed_asset[1];

      if(is_xtz) then (
        do_require(transferred = (pbb_bid.bid_amount * 1utz), (AUCTION_BID_AMOUNT_MISMATCH, transferred, pbb_bid.bid_amount * 1utz));
      );

      if(is_buyout = true) then (
        const origin = pbb_asset_seller;
        transfer 0tz to auction_storage call remove_bundle_auction<bytes * address>((pbb_bundle, pbb_asset_seller));
        transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          bundle,
          auction_storage,
          pbb_bid.bid_bidder,
          pbb_bid.bid_amount,
          auction.bar_buy_asset_type,
          auction.bar_buy_asset,
          caller,
          pbb_asset_seller,
          auction_storage,
          protocol_fee,
          auction.bar_origin_fees.concat(pbb_bid.bid_origin_fees),
          auction.bar_payouts.concat(pbb_bid.bid_payouts))
        );

      ) else (
        if(is_xtz = false) then (
            if(
              auction.bar_end_time - now < extension_duration and
              auction.bar_end_time + extension_duration < auction.bar_start_time + max_duration
            ) then (
              auction.bar_end_time := now + extension_duration;
            );
            auction.bar_last_bid := some(pbb_bid);
            transfer transferred to auction_storage call set_bundle_auction<bytes * address * bundle_auction_record>((pbb_bundle, pbb_asset_seller, auction));
            transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([
              {
                ptp_asset_type = auction.bar_buy_asset_type;
                ptp_asset = auction.bar_buy_asset;
                ptp_amount = pbb_bid.bid_amount;
                ptp_origin = pbb_bid.bid_bidder;
                ptp_destination = auction_storage
              }
            ]);
          )
      );

      if(is_none(previous_bid)) then (
        do_require(auction.bar_minimal_price <= pbb_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(auction.bar_minimal_step <= pbb_bid.bid_amount, AUCTION_BID_TOO_LOW);
      ) else (
        const last_bid ?= previous_bid : NOT_FOUND;
        do_require(last_bid.bid_amount < pbb_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(auction.bar_minimal_step <= pbb_bid.bid_amount - last_bid.bid_amount, AUCTION_BID_TOO_LOW);
        do_require(last_bid.bid_bidder <> pbb_bid.bid_bidder, AUCTION_BID_ALREADY_EXISTS);
        transfer 0tz to auction_storage call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = auction.bar_buy_asset_type;
            ptp_asset = auction.bar_buy_asset;
            ptp_amount = last_bid.bid_amount;
            ptp_origin = auction_storage;
            ptp_destination = last_bid.bid_bidder
          }]
        );
      );
    }
}


entry finish_auction(fa_asset_contract: address, fa_asset_id: nat, fa_asset_seller: address){
    require {
      r_fa_0: transferred = 0utz;
    }
    effect {
      const auction ?= get_auction_record(fa_asset_contract, fa_asset_id, fa_asset_seller) : MISSING_AUCTION;

      do_require(auction.auction_start_time < auction.auction_end_time < now, AUCTION_NOT_FINISHABLE);

      const last_bid ?= auction.auction_last_bid : NO_BID_FOR_AUCTION;

      transfer 0tz to auction_storage call remove_auction<address * nat * address>((fa_asset_contract, fa_asset_id, fa_asset_seller));

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          [{
            bundle_item_contract = fa_asset_contract;
            bundle_item_id = fa_asset_id ;
            bundle_item_qty = auction.auction_sell_asset_amount
          }],
          auction_storage,
          last_bid.bid_bidder,
          last_bid.bid_amount,
          auction.auction_buy_asset_type,
          auction.auction_buy_asset,
          auction_storage,
          fa_asset_seller,
          auction_storage,
          protocol_fee,
          auction.auction_origin_fees.concat(last_bid.bid_origin_fees),
          auction.auction_payouts.concat(last_bid.bid_payouts)
        )
      );

    }
}

entry finish_bundle_auction(fba_bundle: bytes, fba_seller: address){
    require {
      r_fba_0: transferred = 0utz;
    }
    effect {
      const auction ?= get_bundle_auction_record(fba_bundle, fba_seller) : MISSING_AUCTION;
      const bundle = verify_and_unpack_bundle(fba_bundle);

      do_require(auction.bar_start_time < auction.bar_end_time < now, AUCTION_NOT_FINISHABLE);

      const last_bid ?= auction.bar_last_bid : NO_BID_FOR_AUCTION;

      transfer 0tz to auction_storage call remove_bundle_auction<bytes * address>((fba_bundle, fba_seller));

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          bundle,
          auction_storage,
          last_bid.bid_bidder,
          last_bid.bid_amount,
          auction.bar_buy_asset_type,
          auction.bar_buy_asset,
          auction_storage,
          fba_seller,
          auction_storage,
          protocol_fee,
          auction.bar_origin_fees.concat(last_bid.bid_origin_fees),
          auction.bar_payouts.concat(last_bid.bid_payouts)
        )
      );
    }
}

entry cancel_auction(ca_asset_contract: address, ca_asset_id: nat){
    require {
      r_ca_0: transferred = 0utz;
    }
    effect {
      const auction ?= get_auction_record(ca_asset_contract, ca_asset_id, caller) : MISSING_AUCTION;

      do_require(is_none(auction.auction_last_bid), AUCTION_WITH_BID_NON_CANCELLABLE);

      transfer 0tz to auction_storage call remove_auction<address * nat * address>((ca_asset_contract, ca_asset_id, caller));

      transfer 0tz to auction_storage call process_transfers<list<process_transfer_param>>([{
        ptp_asset_type = FA2;
        ptp_asset = pack({
            fa2_asset_contract = ca_asset_contract;
            fa2_asset_token_id = ca_asset_id
            });
        ptp_amount = auction.auction_sell_asset_amount;
        ptp_origin = auction_storage;
        ptp_destination = caller
      }]);
    }
}

entry cancel_bundle_auction(cba_bundle: bytes){
    require {
      r_cba_0: transferred = 0utz;
    }
    effect {
      const auction ?= get_bundle_auction_record(cba_bundle, caller) : MISSING_AUCTION;
      const bundle = verify_and_unpack_bundle(cba_bundle);

      do_require(is_none(auction.bar_last_bid), AUCTION_WITH_BID_NON_CANCELLABLE);

      transfer 0tz to auction_storage call remove_bundle_auction<bytes * address>((cba_bundle, caller));

      for item in bundle do
        transfer 0tz to auction_storage call process_transfers<list<process_transfer_param>>([{
          ptp_asset_type = FA2;
          ptp_asset = pack({
              fa2_asset_contract = item.bundle_item_contract;
              fa2_asset_token_id = item.bundle_item_id
              });
          ptp_amount = item.bundle_item_qty;
          ptp_origin = auction_storage;
          ptp_destination = caller
        }]);
      done;

    }
}

function auction_exists(ae_asset_contract: address, ae_asset_id: nat, ae_asset_seller: address) : bool {
    return match call_view<bool>(
        auction_storage,
        "auction_exists",
        (
            ae_asset_contract,
            ae_asset_id,
            ae_asset_seller
        )
    )
    with
        | some(v) -> v
        | none -> false
    end
}

function bundle_auction_exists(bae_bundle: bytes, bae_seller: address) : bool {
    return match call_view<bool>(
        auction_storage,
        "bundle_auction_exists",
        (
            bae_bundle,
            bae_seller
        )
    )
    with
        | some(v) -> v
        | none -> false
    end
}

function get_auction_record(gar_asset_contract: address, gar_asset_id: nat, gar_asset_seller: address) : option<auction_record> {
    const res ?= call_view<option<auction_record>>(
        auction_storage,
        "get_auction",
        (
            gar_asset_contract,
            gar_asset_id,
            gar_asset_seller
        )
    ) : NOT_FOUND;
    return res
}

function get_bundle_auction_record(gbar_bundle: bytes, gbar_seller: address) : option<bundle_auction_record> {
    const res ?= call_view<option<bundle_auction_record>>(
        auction_storage,
        "get_bundle_auction",
        (
            gbar_bundle,
            gbar_seller
        )
    ) : NOT_FOUND;
    return res
}

function verify_and_unpack_asset(ua_asset_type: asset_type, ua_asset_data: bytes) : (option<address> * option<nat>) {
    var buy_asset_contract: option<address> = none;
    var buy_asset_token_id: option<nat> = none;
    match ua_asset_type with
      | FA2 -> (
        match unpack<FA2_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa2_asset_contract);
            buy_asset_token_id := some(buyAsset.fa2_asset_token_id);
          )
        | none -> fail(CANT_UNPACK_FA2_BUY_ASSET)
        end
      )
      | FA12 -> (
        match unpack<FA12_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa12_asset_contract);
        )
        | none -> fail(CANT_UNPACK_FA12_BUY_ASSET)
        end
      )
      | XTZ -> (
          if(ua_asset_data <> 0x) then (
            fail(WRONG_BUY_ASSET_PAYLOAD)
          )
      )
      end;
      return ((buy_asset_contract, buy_asset_token_id))
}

function verify_and_unpack_bundle(vaub_data: bytes) : (list<bundle_item>) {
    var bundle: list<bundle_item> = [];
    match unpack<list<bundle_item>>(vaub_data) with
    | some(unpacked_bundle) -> (
        bundle := unpacked_bundle;
    )
    | none -> fail(CANT_UNPACK_BUNDLE)
    end;
    do_require(length(bundle) > 0, BUNDLE_CANT_BE_EMPTY);
    for item in bundle do
      do_require(item.bundle_item_qty > 0, INVALID_BUNDLE_ITEM_QTY);
    done;
    return (bundle)
}

function process_sum_fees(psf_fees: list<part>): nat {
  var total = 0;
  for fee in psf_fees do
    total := total + fee.part_value;
  done;
  return total
}