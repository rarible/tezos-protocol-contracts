archetype bids(
    owner: address,
    protocol_fee: nat,
    transfer_manager: address,
    bids_storage: address
)
with metadata ""

constant BID_AMOUNT_MISMATCH            : string = "BID_AMOUNT_MISMATCH"
constant BUNDLE_CANT_BE_EMPTY           : string = "BUNDLE_CANT_BE_EMPTY"
constant CANT_UNPACK_BUNDLE             : string = "CANT_UNPACK_BUNDLE"
constant CANT_UNPACK_FA12_ASSET         : string = "CANT_UNPACK_FA12_ASSET"
constant CANT_UNPACK_FA2_ASSET          : string = "CANT_UNPACK_FA2_ASSET"
constant EXPIRED_BID                    : string = "EXPIRED_BID"
constant INVALID_BUNDLE_ITEM_QTY        : string = "INVALID_BUNDLE_ITEM_QTY"
constant INVALID_CALLER                 : string = "INVALID_CALLER"
constant MAX_BUNDLE_SIZE                : string = "MAX_BUNDLE_SIZE"
constant MISSING_BID                    : string = "MISSING_BID"
constant MISSING_OWNER_CANDIDATE        : string = "MISSING_OWNER_CANDIDATE"
constant NOT_FOUND                      : string = "NOT_FOUND"
constant WRONG_XTZ_PAYLOAD              : string = "WRONG_XTZ_PAYLOAD"
constant INVALID_NAT                    : string = "INVALID_NAT"

//----------------------------------------------------------------------------
// Owner role transfer
//----------------------------------------------------------------------------

variable owner_candidate : option<address> = none
variable max_bundle_items : nat = 10
variable default_bid_duration: duration = 1w

entry declare_ownership(candidate : address) {
  called by owner
  require {
    r_dc_0: transferred = 0utz;
  }
  effect {
    owner_candidate := some(candidate);
  }
}

entry claim_ownership() {
  constant {
    owner_candidate_addr ?is owner_candidate otherwise MISSING_OWNER_CANDIDATE;
  }
  require {
    r_co_0: owner_candidate_addr = caller otherwise INVALID_CALLER;
    r_co_1: transferred = 0utz;
  }
  effect {
    owner := owner_candidate_addr;
    owner_candidate := none
  }
}


//----------------------------------------------------------------------------
// Admin
//----------------------------------------------------------------------------
entry set_bids_storage_contract(sbsc_contract : address) {
  called by owner
  require {
    r_sbs_0: transferred = 0utz;
  }
  effect {
    bids_storage := sbsc_contract;
  }
}

entry set_protocol_fee(spf : nat) {
  called by owner
  require {
    r_spf_0: transferred = 0utz;
  }
  effect {
    protocol_fee := spf;
  }
}

entry set_max_bundle_items(smbi_number : nat) {
  called by owner
  effect {
    max_bundle_items := smbi_number;
  }
}

entry set_transfer_manager(stm_contract : address) {
  called by owner
  require {
    r_stm_0: transferred = 0utz;
  }
  effect {
    transfer_manager := stm_contract;
  }
}

entry set_default_bid_duration(dbd : duration) {
  called by owner
  require {
    r_sdbd_0: transferred = 0utz;
  }
  effect {
    default_bid_duration := dbd;
  }
}


//----------------------------------------------------------------------------
// Types
//----------------------------------------------------------------------------
enum asset_type =
| XTZ
| FA12
| FA2


record FA2_asset {
  fa2_asset_contract : address;
  fa2_asset_token_id: nat;
}

record FA12_asset {
  fa12_asset_contract : address;
}

record transfer_param {
  destination_address: address;
  token_id: nat;
  token_amount: nat
} as ((%to, (token_id, amount)))

record process_transfer_param {
  ptp_asset_type: asset_type;
  ptp_asset: bytes;
  ptp_amount: nat;
  ptp_origin: address;
  ptp_destination: address;
}

record part {
  part_account : address;
  part_value   : nat;
}

record fees {
  origin_fees : list<part>;
  payouts: list<part>;
}

record bid {
  bid_origin_fees : list<part>;
  bid_payouts : list<part>;
  bid_amount : nat;
  bid_asset_qty: nat;
  bid_expiry_date : option<date>;
  bid_data_type  : option<bytes>;
  bid_data       : option<bytes>;
}

record bundle_item {
  bundle_item_contract : address;
  bundle_item_id   : nat;
  bundle_item_qty: nat;
}

record bundle_bid {
  bundle_bid_origin_fees : list<part>;
  bundle_bid_payouts : list<part>;
  bundle_bid_amount : nat;
  bundle_bid_expiry_date : option<date>;
  bundle_bid_data_type  : option<bytes>;
  bundle_bid_data       : option<bytes>;
}

entry put_bid(
  pb_asset_contract: address,
  pb_asset_token_id: nat,
  pb_bid_type: asset_type,
  pb_bid_asset: bytes,
  pb_bid : bid) {
    require {
        r_pb0: pb_bid.bid_amount > 0;
        r_pb2: pb_bid.bid_asset_qty > 0;
        r_pb3: pb_bid_type = FA2
            or
            pb_bid_type = FA12
            or
            pb_bid_type = XTZ;
    }
    effect {
      const current_bid = get_bid(pb_asset_contract, pb_asset_token_id, caller, pb_bid_type, pb_bid_asset);
      const is_xtz = (pb_bid_type = XTZ);
      const unpack_result = verify_and_unpack_asset(pb_bid_type, pb_bid_asset);

      if(is_none(pb_bid.bid_expiry_date)) then (
        pb_bid.bid_expiry_date := some((now + default_bid_duration));
      );

      transfer transferred to bids_storage call set_bid<address * nat * address * asset_type * bytes * bid>(
        (
          pb_asset_contract,
          pb_asset_token_id,
          caller,
          pb_bid_type,
          pb_bid_asset,
          pb_bid
        )
      );

      if(is_some(current_bid)) then (
          const current_bid_data ?= current_bid : NOT_FOUND;
          transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pb_bid_type;
            ptp_asset = pb_bid_asset;
            ptp_amount = current_bid_data.bid_amount;
            ptp_origin = bids_storage;
            ptp_destination = caller
          }]);
      );

      if(not is_xtz) then (
        do_require(transferred = 0utz, (BID_AMOUNT_MISMATCH, transferred, 0));
        transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pb_bid_type;
            ptp_asset = pb_bid_asset;
            ptp_amount = pb_bid.bid_amount;
            ptp_origin = caller;
            ptp_destination = bids_storage
        }]);
      ) else (
        do_require(transferred = (pb_bid.bid_amount * 1utz), (BID_AMOUNT_MISMATCH, transferred, pb_bid.bid_amount * 1utz));
      );
    }
}

entry put_floor_bid(
  pfb_asset_contract: address,
  pfb_bid_type: asset_type,
  pfb_bid_asset: bytes,
  pfb_bid : bid) {
    require {
        r_pfb0: pfb_bid.bid_amount > 0;
        r_pfb2: pfb_bid.bid_asset_qty > 0;
        r_pfb3: pfb_bid_type = FA2
            or
            pfb_bid_type = FA12
            or
            pfb_bid_type = XTZ;
    }
    effect {
      const current_bid = get_floor_bid(pfb_asset_contract, caller, pfb_bid_type, pfb_bid_asset);
      const is_xtz = (pfb_bid_type = XTZ);
      const unpack_result = verify_and_unpack_asset(pfb_bid_type, pfb_bid_asset);

      if(is_none(pfb_bid.bid_expiry_date)) then (
        pfb_bid.bid_expiry_date := some((now + default_bid_duration));
      );

      transfer transferred to bids_storage call set_floor_bid<address * address * asset_type * bytes * bid>(
        (
          pfb_asset_contract,
          caller,
          pfb_bid_type,
          pfb_bid_asset,
          pfb_bid
        )
      );

      if(is_some(current_bid)) then (
          const current_bid_data ?= current_bid : NOT_FOUND;
          transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pfb_bid_type;
            ptp_asset = pfb_bid_asset;
            ptp_amount = current_bid_data.bid_amount;
            ptp_origin = bids_storage;
            ptp_destination = caller
          }]);
      );

      if(not is_xtz) then (
        do_require(transferred = 0utz, (BID_AMOUNT_MISMATCH, transferred, 0));
        transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pfb_bid_type;
            ptp_asset = pfb_bid_asset;
            ptp_amount = pfb_bid.bid_amount;
            ptp_origin = caller;
            ptp_destination = bids_storage
        }]);
      ) else (
        do_require(transferred = (pfb_bid.bid_amount * 1utz), (BID_AMOUNT_MISMATCH, transferred, pfb_bid.bid_amount * 1utz));
      );
    }
}

entry put_bundle_bid(
  pbb_bundle: bytes,
  pbb_bid_type: asset_type,
  pbb_bid_asset: bytes,
  pbb_bid : bundle_bid) {
    require {
        r_pbb0: pbb_bid.bundle_bid_amount > 0;
        r_pbb1: pbb_bid_type = FA2
            or
            pbb_bid_type = FA12
            or
            pbb_bid_type = XTZ;
    }
    effect {
      const current_bid = get_bundle_bid(pbb_bundle, caller, pbb_bid_type, pbb_bid_asset);
      const is_xtz = (pbb_bid_type = XTZ);
      const unpack_result = verify_and_unpack_asset(pbb_bid_type, pbb_bid_asset);
      const bundle = verify_and_unpack_bundle(pbb_bundle);
      do_require(length(bundle) <= max_bundle_items, (MAX_BUNDLE_SIZE, max_bundle_items));

      if(is_none(pbb_bid.bundle_bid_expiry_date)) then (
        pbb_bid.bundle_bid_expiry_date := some((now + default_bid_duration));
      );

      transfer transferred to bids_storage call set_bundle_bid<bytes * address * asset_type * bytes * bundle_bid>(
        (
          pbb_bundle,
          caller,
          pbb_bid_type,
          pbb_bid_asset,
          pbb_bid
        )
      );

      if(is_some(current_bid)) then (
          const current_bid_data ?= current_bid : NOT_FOUND;
          transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pbb_bid_type;
            ptp_asset = pbb_bid_asset;
            ptp_amount = current_bid_data.bundle_bid_amount;
            ptp_origin = bids_storage;
            ptp_destination = caller
          }]);
      );

      if(not is_xtz) then (
        do_require(transferred = 0utz, (BID_AMOUNT_MISMATCH, transferred, 0));
        transfer 0tz to transfer_manager call process_transfers<list<process_transfer_param>>([{
            ptp_asset_type = pbb_bid_type;
            ptp_asset = pbb_bid_asset;
            ptp_amount = pbb_bid.bundle_bid_amount;
            ptp_origin = caller;
            ptp_destination = bids_storage
        }]);
      ) else (
        do_require(transferred = (pbb_bid.bundle_bid_amount * 1utz), (BID_AMOUNT_MISMATCH, transferred, pbb_bid.bundle_bid_amount * 1utz));
      );
    }
}

entry accept_bid(
  ab_asset_contract: address,
  ab_asset_token_id: nat,
  ab_bidder: address,
  ab_bid_type: asset_type,
  ab_bid_asset: bytes,
  ab_origin_fees: list<part>,
  ab_payouts: list<part>) {
    require {
      r_ab_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_bid(ab_asset_contract, ab_asset_token_id, ab_bidder, ab_bid_type, ab_bid_asset) : MISSING_BID;

      do_require(current_bid.bid_expiry_date ? the > now : true, EXPIRED_BID);

      transfer 0tz to bids_storage call remove_bid<address * nat * address * asset_type * bytes>((ab_asset_contract, ab_asset_token_id, ab_bidder, ab_bid_type, ab_bid_asset));

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          [{
            bundle_item_contract = ab_asset_contract;
            bundle_item_id = ab_asset_token_id ;
            bundle_item_qty = current_bid.bid_asset_qty
          }],
          caller,
          ab_bidder,
          current_bid.bid_amount,
          ab_bid_type,
          ab_bid_asset,
          bids_storage,
          caller,
          bids_storage,
          protocol_fee,
          current_bid.bid_origin_fees.concat(ab_origin_fees),
          current_bid.bid_payouts.concat(ab_payouts)
        )
      );
    }
}

entry accept_floor_bid(
  afb_asset_contract: address,
  afb_asset_token_id: nat,
  afb_bidder: address,
  afb_bid_type: asset_type,
  afb_bid_asset: bytes,
  afb_origin_fees: list<part>,
  afb_payouts: list<part>){
    require {
      r_afb_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_floor_bid(afb_asset_contract, afb_bidder, afb_bid_type, afb_bid_asset) : MISSING_BID;

      do_require(current_bid.bid_expiry_date ? the > now : true, EXPIRED_BID);

      transfer 0tz to bids_storage call remove_floor_bid<address * address * asset_type * bytes>((afb_asset_contract, afb_bidder, afb_bid_type, afb_bid_asset));

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          [{
            bundle_item_contract = afb_asset_contract;
            bundle_item_id = afb_asset_token_id ;
            bundle_item_qty = current_bid.bid_asset_qty
          }],
          caller,
          afb_bidder,
          current_bid.bid_amount,
          afb_bid_type,
          afb_bid_asset,
          bids_storage,
          caller,
          bids_storage,
          protocol_fee,
          current_bid.bid_origin_fees.concat(afb_origin_fees),
          current_bid.bid_payouts.concat(afb_payouts)
        )
      );
    }
}

entry accept_bundle_bid(
  abb_bundle: bytes,
  abb_bidder: address,
  abb_bid_type: asset_type,
  abb_bid_asset: bytes,
  abb_origin_fees: list<part>,
  abb_payouts: list<part>){
    require {
      r_abb_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_bundle_bid(abb_bundle, abb_bidder, abb_bid_type, abb_bid_asset) : MISSING_BID;
      const unpacked_bundle = verify_and_unpack_bundle(abb_bundle);

      do_require(current_bid.bundle_bid_expiry_date ? the > now : true, EXPIRED_BID);

      transfer 0tz to bids_storage call remove_bundle_bid<bytes * address * asset_type * bytes>((abb_bundle, abb_bidder, abb_bid_type, abb_bid_asset));

      transfer transferred to transfer_manager call manage_transfers<list<bundle_item> * address * address * nat * asset_type * bytes * address * address * address * nat * list<part> * list<part>>(
        (
          unpacked_bundle,
          caller,
          abb_bidder,
          current_bid.bundle_bid_amount,
          abb_bid_type,
          abb_bid_asset,
          bids_storage,
          caller,
          bids_storage,
          protocol_fee,
          current_bid.bundle_bid_origin_fees.concat(abb_origin_fees),
          current_bid.bundle_bid_payouts.concat(abb_payouts)
        )
      );
    }
}

entry cancel_bid(
  cb_asset_contract: address,
  cb_asset_token_id: nat,
  cb_bid_type: asset_type,
  cb_bid_asset: bytes){
    require {
      r_cb_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_bid(cb_asset_contract, cb_asset_token_id, caller, cb_bid_type, cb_bid_asset) : MISSING_BID;

      transfer 0tz to bids_storage call remove_bid<address * nat * address * asset_type * bytes>((cb_asset_contract, cb_asset_token_id, caller, cb_bid_type, cb_bid_asset));

      transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
        ptp_asset_type = cb_bid_type;
        ptp_asset = cb_bid_asset;
        ptp_amount = current_bid.bid_amount;
        ptp_origin = bids_storage;
        ptp_destination = caller
      }]);
    }
}

entry cancel_floor_bid(
  cfb_asset_contract: address,
  cfb_bid_type: asset_type,
  cfb_bid_asset: bytes){
    require {
      r_cfb_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_floor_bid(cfb_asset_contract, caller, cfb_bid_type, cfb_bid_asset) : MISSING_BID;

      transfer 0tz to bids_storage call remove_floor_bid<address * address * asset_type * bytes>((cfb_asset_contract, caller, cfb_bid_type, cfb_bid_asset));

      transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
        ptp_asset_type = cfb_bid_type;
        ptp_asset = cfb_bid_asset;
        ptp_amount = current_bid.bid_amount;
        ptp_origin = bids_storage;
        ptp_destination = caller
      }]);
    }
}

entry cancel_bundle_bid(
  cbb_bundle: bytes,
  cbb_bid_type: asset_type,
  cbb_bid_asset: bytes){
    require {
      r_cbb_0: transferred = 0utz;
    }
    effect {
      const current_bid ?= get_bundle_bid(cbb_bundle, caller, cbb_bid_type, cbb_bid_asset) : MISSING_BID;

      transfer 0tz to bids_storage call remove_bundle_bid<bytes * address * asset_type * bytes>((cbb_bundle, caller, cbb_bid_type, cbb_bid_asset));

      transfer 0tz to bids_storage call process_transfers<list<process_transfer_param>>([{
        ptp_asset_type = cbb_bid_type;
        ptp_asset = cbb_bid_asset;
        ptp_amount = current_bid.bundle_bid_amount;
        ptp_origin = bids_storage;
        ptp_destination = caller
      }]);
    }
}

function verify_and_unpack_asset(ua_asset_type: asset_type, ua_asset_data: bytes) : (option<address> * option<nat>) {
    var buy_asset_contract: option<address> = none;
    var buy_asset_token_id: option<nat> = none;
    match ua_asset_type with
      | FA2 -> (
        match unpack<FA2_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa2_asset_contract);
            buy_asset_token_id := some(buyAsset.fa2_asset_token_id);
          )
        | none -> fail(CANT_UNPACK_FA2_ASSET)
        end
      )
      | FA12 -> (
        match unpack<FA12_asset>(ua_asset_data) with
        | some(buyAsset) -> (
            buy_asset_contract := some(buyAsset.fa12_asset_contract);
        )
        | none -> fail(CANT_UNPACK_FA12_ASSET)
        end
      )
      | XTZ -> (
          if(ua_asset_data <> 0x) then (
            fail(WRONG_XTZ_PAYLOAD)
          )
      )
      end;
      return ((buy_asset_contract, buy_asset_token_id))
}

function verify_and_unpack_bundle(vaub_data: bytes) : (list<bundle_item>) {
    var bundle : list<bundle_item> = [];
    match unpack<list<bundle_item>>(vaub_data) with
    | some(unpacked_bundle) -> (
        bundle := unpacked_bundle;
    )
    | none -> fail(CANT_UNPACK_BUNDLE)
    end;
    do_require(length(bundle) > 0, BUNDLE_CANT_BE_EMPTY);
    for item in bundle do
      do_require(item.bundle_item_qty > 0, INVALID_BUNDLE_ITEM_QTY);
    done;
    return (bundle)
}

function get_bid(
  gb_asset_contract: address,
  gb_asset_token_id: nat,
  gb_bidder: address,
  gb_bid_type: asset_type,
  gb_bid_asset: bytes) : option<bid> {
    const res ?= call_view<option<bid>>(
        bids_storage,
        "get_bid",
        (
          gb_asset_contract,
          gb_asset_token_id,
          gb_bidder,
          gb_bid_type,
          gb_bid_asset
        )
    ) : NOT_FOUND;
    return res
}

function get_floor_bid(
  gb_asset_contract: address,
  gb_bidder: address,
  gb_bid_type: asset_type,
  gb_bid_asset: bytes) : option<bid> {
    const res ?= call_view<option<bid>>(
        bids_storage,
        "get_floor_bid",
        (
          gb_asset_contract,
          gb_bidder,
          gb_bid_type,
          gb_bid_asset
        )
    ) : NOT_FOUND;
    return res
}

function get_bundle_bid(
  gbb_bundle: bytes,
  gbb_bidder: address,
  gbb_bid_type: asset_type,
  gbb_bid_asset: bytes) : option<bundle_bid> {
    const res ?= call_view<option<bundle_bid>>(
        bids_storage,
        "get_bundle_bid",
        (
          gbb_bundle,
          gbb_bidder,
          gbb_bid_type,
          gbb_bid_asset
        )
    ) : NOT_FOUND;
    return res
}